#!/usr/bin/env zsh

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.dotfiles"
export EDITOR='vim'
export VEDITOR='code'
export PATH="$PATH:$DOTFILES/bin:$HOME/.bin"
export GPG_TTY=$(tty)

if [[ "${OSTYPE#*darwin}" = "$OSTYPE" ]]; then
  is_mac_os=false
else
  is_mac_os=true
fi

source $DOTFILES/lib/io_handlers.sh

HISTFILE=~/.zsh_history
HISTSIZE=1000000000
SAVEHIST=1000000000


log_info 'Configuring ZSH'

# Basic auto/tab complete:
autoload -Uz compinit
zmodload zsh/complist
compinit
_comp_options+=(globdots)

zstyle ':completion:*' completer _oldlist _expand _complete _match _ignored _approximate # forces zsh to realize new commands
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}' # matches case insensitive for lowercase
zstyle ':completion:*' insert-tab pending # pasting with tabs doesn't perform completion
zstyle ':completion:*' rehash true # rehash if command not found (possibly recently installed)
zstyle ':completion:*' menu select=2 # menu if nb items > 2

# load zsh files
zsh_files=($DOTFILES/zsh/*.zsh)

for file in $zsh_files; do
  source "$file"
done

unset zsh_files 

# Plugins
source /usr/share/zsh/plugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
source /usr/share/zsh/plugins/zsh-autosuggestions/zsh-autosuggestions.zsh

if $is_mac_os; then
	log_info 'Configuring homebrew for MacOS'
	source $DOTFILES/homebrew/config.sh
fi

log_info 'Setting up fuzzy search'
# search history with fzf if installed, default otherwise
if command -v fzf > /dev/null; then
	if $is_mac_os; then
		. $HOME/.fzf.zsh
	else
		. /usr/share/fzf/key-bindings.zsh
	fi
else
	bindkey '^R' history-incremental-search-backward
fi

log_info 'Loading .localrc configuration'
[ -f ~/.localrc ] && . ~/.localrc

# if has nvm installed, use it when entering node project
if command -v node > /dev/null; then
	log_info 'Configuring NVM'
	source $DOTFILES/frameworks/nvm_init.sh
fi

log_info 'Configuring Starship for prompt'
export STARSHIP_CONFIG="$DOTFILES/zsh/starship/starship.toml"
export STARSHIP_CACHE="$DOTFILES/zsh/starship/cache"
prompt off
eval "$(starship init zsh)"

# export ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets pattern cursor)

log_success 'Good to go!'
